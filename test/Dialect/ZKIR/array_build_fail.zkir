// RUN: zkir-opt %s -split-input-file -verify-diagnostics

func.func private @constant_scalar_mismatch() {
  %a = zkir.feltconst 6744573456376
  %b = index.constant 2 // expected-note {{prior use here}}
  // expected-error@+1 {{use of value '%b' expects different type than prior uses: '!zkir.felt' vs 'index'}}
  %r = zkir.new_array %a, %b : !zkir.array<2 x !zkir.felt>
}

// -----

func.func private @param_scalar_mismatch(
      %a: !zkir.felt,
      %b: !zkir.felt,
      %c: index // expected-note {{prior use here}}
      ) {
  // expected-error@+1 {{use of value '%c' expects different type than prior uses: '!zkir.felt' vs 'index'}}
  %r = zkir.new_array %a, %b, %c: !zkir.array<3 x !zkir.felt>
}

// -----

func.func private @arr_size_mismatch(%a: !zkir.array<3 x index>) {
  %c = index.constant 23435643245
  %b = zkir.new_array %c: !zkir.array<1 x index> // expected-note {{prior use here}}
  // expected-error@+1 {{use of value '%b' expects different type than prior uses: '!zkir.array<3 x index>' vs '!zkir.array<1 x index>'}}
  %r = zkir.new_array %a, %b: !zkir.array<2 x !zkir.array<3 x index>>
}

// -----

func.func private @arr_type_mismatch(
      %a: !zkir.array<3 x index>,
      %b: !zkir.array<3 x !zkir.felt> // expected-note {{prior use here}}
      ) {
  // expected-error@+1 {{use of value '%b' expects different type than prior uses: '!zkir.array<3 x index>' vs '!zkir.array<3 x !zkir.felt>'}}
  %r = zkir.new_array %a, %b: !zkir.array<2 x !zkir.array<3 x index>>
}

// -----

func.func private @mismatch_to_type_attr(
      %a: !zkir.array<3 x index>, // expected-note {{prior use here}}
      %b: !zkir.felt
      ) {
  %c = index.constant 324
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: 'index' vs '!zkir.array<3 x index>'}}
  %r = zkir.new_array %a, %b, %c: !zkir.array<3 x index>
}

// -----

func.func private @inner_array_type_mismatch_to_type_attr(
      %a: !zkir.array<3 x !zkir.felt>, // expected-note {{prior use here}}
      %b: !zkir.array<3 x !zkir.felt>
      ) {
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!zkir.array<3 x index>' vs '!zkir.array<3 x !zkir.felt>'}}
  %r = zkir.new_array %a, %b: !zkir.array<2 x !zkir.array<3 x index>>
}

// -----

func.func private @mismatch_to_type_attr_size() {
  %a = zkir.feltconst 6744573456376
  %b = zkir.feltconst 381928390103509
  // expected-error@+1 {{custom op 'zkir.new_array' 2 operands present, but expected 3}}
  %r = zkir.new_array %a, %b : !zkir.array<3 x !zkir.felt>
}

// -----

func.func private @inner_array_type_mismatch_to_type_attr_size(
      %a: !zkir.array<3 x index>, // expected-note {{prior use here}}
      %b: !zkir.array<3 x index>
      ) {
  // expected-error@+1 {{use of value '%a' expects different type than prior uses: '!zkir.array<233 x index>' vs '!zkir.array<3 x index>'}}
  %r = zkir.new_array %a, %b: !zkir.array<2 x !zkir.array<233 x index>>
}

// -----
